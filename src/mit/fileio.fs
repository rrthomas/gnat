\ Mass storage input/output words
\
\ (c) Reuben Thomas 1996-2020
\
\ The package is distributed under the GNU GPL version 3, or, at your
\ option, any later version.
\
\ THIS PROGRAM IS PROVIDED AS IS, WITH NO WARRANTY. USE IS AT THE USERâ€™S
\ RISK.

ALSO ASSEMBLER

 0 LIBC-PRIMITIVE STRLEN
 1 LIBC-PRIMITIVE STRNCPY
 2 LIBC-PRIMITIVE STDIN
 3 LIBC-PRIMITIVE STDOUT
 4 LIBC-PRIMITIVE STDERR
 5 LIBC-PRIMITIVE R/O
 6 LIBC-PRIMITIVE W/O
 7 LIBC-PRIMITIVE R/W
 8 LIBC-PRIMITIVE O_CREAT
 9 LIBC-PRIMITIVE O_TRUNC
10 LIBC-PRIMITIVE OPEN
11 LIBC-PRIMITIVE CLOSE-FILE
12 LIBC-PRIMITIVE READ
13 LIBC-PRIMITIVE WRITE
14 LIBC-PRIMITIVE SEEK_SET
15 LIBC-PRIMITIVE SEEK_CUR
16 LIBC-PRIMITIVE SEEK_END
17 LIBC-PRIMITIVE LSEEK
18 LIBC-PRIMITIVE FLUSH-FILE
19 LIBC-PRIMITIVE RENAME
20 LIBC-PRIMITIVE REMOVE
21 LIBC-PRIMITIVE FILE_SIZE
22 LIBC-PRIMITIVE RESIZE_FILE
23 LIBC-PRIMITIVE FILE-STATUS

PREVIOUS


: CREATE-FAM   O_CREAT O_TRUNC OR ;

0 CONSTANT BIN-MODE
: BIN ;

: OPEN-FILE   ( c-addr u fam -- fid ior )   -ROT SCRATCH-C0END SWAP OPEN
    DUP 0 < ;
: READ-FILE   ( c-addr u fileid -- nread ior )   READ  DUP 0 < ;
: WRITE-FILE   ( c-addr u fileid -- ior )   WRITE  0 < ;
: RENAME-FILE   ( c-addr1 u1 c-addr2 u2 -- ior )   SCRATCH-C0END -ROT HERE 256 C0END HERE
   SWAP RENAME ;
: DELETE-FILE   ( c-addr u -- ior )   SCRATCH-C0END REMOVE ;
: CREATE-FILE   ( adr u fam -- fid ior )   CREATE-FAM OR  OPEN-FILE ;
\ FIXME: Next two words depend on ENDISM and sizeof(off_t)
: D>OFF_T ;
: OFF_T>D ;
: FILE-POSITION   0. D>OFF_T SEEK_CUR LSEEK
   OFF_T>D  OVER -1 =  OVER -1 =  AND ;
: REPOSITION-FILE   -ROT  D>OFF_T SEEK_SET LSEEK  OFF_T>D  -1 =  SWAP -1 =  AND ;
: FILE-SIZE   FILE_SIZE  >R OFF_T>D R> ;
: RESIZE-FILE   >R D>OFF_T R>  RESIZE_FILE ;
: ABSOLUTE-ARG   ( u1 -- c-addr u2 )
   TOTAL-ARGS OVER > IF \ u1
      MIT_ARGV  SWAP CELLS + @  DUP STRLEN
   ELSE
      DROP  0 0
   THEN ;