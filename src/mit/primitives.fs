\ (c) Reuben Thomas 2018-2020
\
\ The package is distributed under the GNU GPL version 3, or, at your
\ option, any later version.
\
\ THIS PROGRAM IS PROVIDED AS IS, WITH NO WARRANTY. USE IS AT THE USERâ€™S
\ RISK.

CR .( Required primitives )

INCLUDE" call-cells.fs" CELLS >'FORTH TO PRIMITIVE-RP


\ Stack primitives

1 0 PRIMITIVE DROP
1 MPUSHI MPOP
END-PRIMITIVE

1 1 PRIMITIVE PICK
MDUP
END-PRIMITIVE

2 2 PRIMITIVE SWAP
0 MPUSHI MSWAP
END-PRIMITIVE

0 1 PRIMITIVE CELL
4 MPUSHI
END-PRIMITIVE

0 1 PRIMITIVE -CELL
-4 MPUSHI
END-PRIMITIVE


\ Memory primitives

1 1 PRIMITIVE @
MLOAD
END-PRIMITIVE

2 0 PRIMITIVE !
MSTORE
END-PRIMITIVE

1 1 PRIMITIVE C@
MLOAD1
END-PRIMITIVE

2 0 PRIMITIVE C!
MSTORE1
END-PRIMITIVE


\ Arithmetic and logical primitives

2 1 PRIMITIVE +
MADD
END-PRIMITIVE

1 1 PRIMITIVE NEGATE
MNEGATE
END-PRIMITIVE

2 1 PRIMITIVE *
MMUL
END-PRIMITIVE

2 2 PRIMITIVE U/MOD
MUDIVMOD 0 MPUSHI MSWAP
END-PRIMITIVE

2 2 PRIMITIVE S/REM
MDIVMOD 0 MPUSHI MSWAP
END-PRIMITIVE

2 1 PRIMITIVE =
MXOR 1 MPUSHI MULT MNEGATE
END-PRIMITIVE

2 1 PRIMITIVE <
MLT
MNEGATE
END-PRIMITIVE

2 1 PRIMITIVE U<
MULT
MNEGATE
END-PRIMITIVE

2 1 PRIMITIVE AND
MAND
END-PRIMITIVE

2 1 PRIMITIVE OR
MOR
END-PRIMITIVE

2 1 PRIMITIVE XOR
MXOR
END-PRIMITIVE

1 1 PRIMITIVE INVERT
MNOT
END-PRIMITIVE

2 1 PRIMITIVE LSHIFT
MLSHIFT
END-PRIMITIVE

2 1 PRIMITIVE RSHIFT
MRSHIFT
END-PRIMITIVE


\ System primitives

1 0 PRIMITIVE HALT
MHALT
END-PRIMITIVE


\ Control primitives

INCLUDE" primitive-bracket-does.fs"


\ Stack management

VARIABLE RP
\ FIXME: >R and R> must be defined as CODE words, because they are needed by
\ LINK, and UNLINK,
1 0 PRIMITIVE >R
MPUSHREL 0 MPUSHI MDUP MLOAD
' RP >BODY OFFSET,
-4 MPUSHI MADD 0 MPUSHI MDUP \ FIXME: target -CELL, not -4
1 MPUSHI MSWAP MSTORE MSTORE
END-PRIMITIVE
0 INLINE \ Prevent inlining: it's too long to go at the start of each word!

0 1 PRIMITIVE R>
MPUSHREL 0 MPUSHI MDUP MLOAD
' RP >BODY OFFSET,
0 MPUSHI MDUP 4 MPUSHI MADD \ FIXME: target CELL, not 4
0 MPUSHI MSWAP 1 MPUSHI MSWAP
MSTORE MLOAD
END-PRIMITIVE
0 INLINE \ Prevent inlining: it's too long to go at the start of each word!

0 1 PRIMITIVE R@
MPUSHREL MLOAD MLOAD
' RP >BODY OFFSET,
END-PRIMITIVE
0 INLINE \ Prevent inlining because of relative offset to RP

0 1 PRIMITIVE RP@
MPUSHREL MLOAD
' RP >BODY OFFSET,
END-PRIMITIVE
0 INLINE \ Prevent inlining because of relative offset to RP

\ FIXME: -9 THROW if RP is out of range
\ Must be a primitive as it would mess up its own return
1 0 PRIMITIVE RP!
MPUSHREL MSTORE NOPALIGN
' RP >BODY OFFSET,
END-PRIMITIVE
0 INLINE \ Prevent inlining because of relative offset to RP

0 1 PRIMITIVE TOTAL-ARGS
MARGC
END-PRIMITIVE

0 1 PRIMITIVE ARGV
MARGV
END-PRIMITIVE


\ Stack management primitives

0 0 PRIMITIVE SP@
MTHIS_STATE MGET_STACK_DEPTH
2 MPUSHI MLSHIFT \ FIXME constant!
END-PRIMITIVE

\ FIXME: -9 THROW if out of range
1 0 PRIMITIVE SP!
2 MPUSHI MRSHIFT \ FIXME constant!
MTHIS_STATE MSET_STACK_DEPTH
END-PRIMITIVE
