\ Bee assembler for pForth
\
\ (c) Reuben Thomas 2020
\
\ The package is distributed under the GNU GPL version 3, or, at your
\ option, any later version.
\
\ THIS PROGRAM IS PROVIDED AS IS, WITH NO WARRANTY. USE IS AT THE USERâ€™S
\ RISK.

CR .( Bee assembler )


VOCABULARY ASSEMBLER  ALSO ASSEMBLER DEFINITIONS

: INLINE   ( char -- )   LAST >INFO 2 + C! ;

FORTH DEFINITIONS
INCLUDE" code.fs"
: END-CODE   ALIGN  PREVIOUS ;
ASSEMBLER DEFINITIONS

: OFFSET   ( from to -- offset )   >-< ;
: OFFSET,   ( to -- )   HERE SWAP OFFSET , ;

0 CONSTANT OP_CALL    1 CONSTANT OP_LITERAL
2 CONSTANT OP_OFFSET  3 CONSTANT OP_INSTRUCTION

: >OPCODE   ( operand type -- )   SWAP  2 LSHIFT  OR ;
: INST   CREATE OP_INSTRUCTION >OPCODE ,  DOES> @ , ;
: BRANCH-INST   CREATE OP_INSTRUCTION >OPCODE ,  DOES> @ ,  OFFSET, ;

: INSTS   SWAP 1+ SWAP DO  I INST  LOOP ;

: BCALL  OP_CALL >OPCODE , ;
: BLITERAL   OP_LITERAL >OPCODE , ;
: BOFFSET   HERE SWAP OFFSET 2 ARSHIFT  OP_OFFSET >OPCODE , ;

 2  0 INSTS BDROP   BPICK   BROLL
 5  3 INSTS B>R     BR>     BR@
 9  6 INSTS BS0@    BS0!    BSP@    BSP!
13 10 INSTS BR0@    BR0!    BRP@    BRP!
15 14 INSTS BMEMORY@ BCELL
19 16 INSTS B@      B!      BC@     BC!
24 20 INSTS B+      BNEGATE B*      BU/MOD  BS/REM
27 25 INSTS B=      B<      BU<
31 28 INSTS BINVERT BAND    BOR     BXOR
33 32 INSTS BLSHIFT BRSHIFT
36 34 INSTS BEXIT   BEXECUTE BHALT

37 BRANCH-INST BBRANCH
38 BRANCH-INST B?BRANCH

39 INST B(LITERAL)

130 128 INSTS BARGC BARGLEN BARGCOPY
133 131 INSTS BSTDIN BSTDOUT BSTDERR
135 134 INSTS BOPEN-FILE BCLOSE-FILE
137 136 INSTS BREAD-FILE BWRITE-FILE
139 138 INSTS BFILE-POSITION BREPOSITION-FILE
142 140 INSTS BFLUSH-FILE BRENAME-FILE BDELETE-FILE
144 143 INSTS BFILE-SIZE BRESIZE-FILE
145 INST   BFILE-STATUS

PREVIOUS DEFINITIONS
