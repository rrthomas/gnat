\ Bee assembler for pForth
\
\ (c) Reuben Thomas 2020
\
\ The package is distributed under the GNU GPL version 3, or, at your
\ option, any later version.
\
\ THIS PROGRAM IS PROVIDED AS IS, WITH NO WARRANTY. USE IS AT THE USERâ€™S
\ RISK.

CR .( Bee assembler )


VOCABULARY ASSEMBLER  ALSO ASSEMBLER DEFINITIONS

: INLINE   ( char -- )   LAST >INFO 2 + C! ;

FORTH DEFINITIONS
INCLUDE" code.fs"
: END-CODE   ALIGN  PREVIOUS ;
ASSEMBLER DEFINITIONS

: OFFSET   ( from to -- offset )   >-< ;
: OFFSET,   ( to -- )   HERE SWAP OFFSET , ;

0 CONSTANT OP_CALL    1 CONSTANT OP_LITERAL
2 CONSTANT OP_OFFSET  3 CONSTANT OP_LEVEL2

0 CONSTANT OP2_JUMPI  1 CONSTANT OP2_JUMPZI
2 CONSTANT OP2_INSN   3 CONSTANT OP2_TRAP

: >OPCODE   ( operand type -- )   SWAP  2 LSHIFT  OR ;
: >OPCODE2   ( operand type -- )   SWAP  2 LSHIFT  OR  2 LSHIFT   OP_LEVEL2 OR ;
: INST   CREATE OP2_INSN >OPCODE2 ,  DOES> @ , ;
: TRAP   CREATE OP2_TRAP >OPCODE2 ,  DOES> @ , ;
: BRANCH-INST   CREATE OP2_INSN >OPCODE2 ,  DOES> @ ,  OFFSET, ;

: INSTS   SWAP 1+ SWAP DO  I INST  LOOP ;

: BCALLI  OP_CALL >OPCODE , ;
: BPUSH   OP_LITERAL >OPCODE , ;
: BPUSHREL   HERE SWAP OFFSET 2 ARSHIFT  OP_OFFSET >OPCODE , ;

 7  0 INSTS BNOP     BNOT     BAND     BOR      BXOR     BLSHIFT  BRSHIFT  BARSHIFT
11  8 INSTS BPOP     BDUP     BSET     BSWAP
12 BRANCH-INST BJUMP
13 BRANCH-INST BJUMPZ
15 14 INSTS BCALL    BRET
23 16 INSTS BLOAD    BSTORE   BLOAD1   BSTORE1  BLOAD2   BSTORE2  BLOAD4   BSTORE4
31 24 INSTS BNEGATE  BADD     BMUL     BDIVMOD  BUDIVMOD BEQ      BLT      BULT
39 32 INSTS BPUSHR   BPOPR    BDUPR    BCATCH   BTHROW   BBREAK   BWORD_BYTES BGET_M0
47 40 INSTS BGET_MSIZE BGET_RSIZE BGET_RP BSET_RP BGET_SSIZE BGET_SP BSET_SP BGET_HANDLER_RP

0 TRAP LIBC

PREVIOUS DEFINITIONS
