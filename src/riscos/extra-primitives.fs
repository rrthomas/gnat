CR .( Extra primitives )

\ Stack primitives

CODE DUP
TOP SP PUSH,
END-SUB

CODE SWAP
R0 SP 0@ LDR,
TOP SP 0@ STR,
TOP R0 MOV,
END-SUB

CODE OVER
TOP SP PUSH,
TOP SP 4 #+@ LDR,
END-SUB

CODE ROT
R0 TOP MOV,
SP @! { R1 TOP } FD LDM,
SP @! { R0 R1 } FD STM,
END-SUB

CODE -ROT
SP @! { R0 R1 } FD LDM,
SP @! { R1 TOP } FD STM,
TOP R0 MOV,
END-SUB

CODE TUCK
R0 SP 0@ LDR,
TOP SP 0@ STR,
R0 SP 4 #-@! STR,
END-SUB

CODE NIP
SP SP 4 # ADD,
END-SUB

CODE ?DUP
TOP 0 # CMP,
TOP SP NE PUSH,
END-SUB


\ Arithmetic and logical primitives

CODE >
R0 SP POP,
R0 TOP CMP,
TOP 0 # GT MVN,
TOP 0 # LE MOV,
END-SUB

CODE <>
R0 SP POP,
TOP TOP R0 SET EOR,
TOP 0 # NE MVN,
END-SUB

CODE 0<
TOP TOP 31 #ASR MOV,
END-SUB

CODE 0>
TOP TOP 0 # RSB,
TOP TOP 31 #ASR MOV,
END-SUB

CODE 0=
TOP TOP 1 # SET SUB,
TOP TOP TOP SBC,
END-SUB

CODE 0<>
TOP 0 # CMP,
TOP 0 # NE MVN,
END-SUB

CODE U>
R0 SP POP,
TOP TOP R0 SET SUB,
TOP TOP TOP SBC,
END-SUB

CODE 0
TOP SP PUSH,
TOP 0 # MOV,
END-SUB

CODE 1
TOP SP PUSH,
TOP 1 # MOV,
END-SUB

CODE -1
TOP SP PUSH,
TOP 0 # MVN,
END-SUB

CODE TRUE
TOP SP PUSH,
TOP FALSE # MVN,
END-SUB

CODE FALSE
TOP SP PUSH,
TOP FALSE # MOV,
END-SUB

CODE 1+
TOP TOP 1 # ADD,
END-SUB

CODE 1-
TOP TOP 1 # SUB,
END-SUB

CODE CELL+
TOP TOP CELL # ADD,
END-SUB

CODE CELL-
TOP TOP CELL # SUB,
END-SUB

CODE 2*
TOP TOP 1 #ASL MOV,
END-SUB

CODE 2/
TOP TOP 1 #ASR MOV,
END-SUB

CODE CELLS
TOP TOP 2 #ASL MOV,
END-SUB

CODE CELL/
TOP TOP 2 #ASR MOV,
END-SUB

CODE 1LSHIFT
TOP TOP 1 #LSL MOV,
END-SUB

CODE 1RSHIFT
TOP TOP 1 #LSR MOV,
END-SUB

CODE >-<
R0 SP 4 #@+ LDR,
TOP TOP R0 SUB,
END-SUB

CODE MAX
R0 SP POP,
R0 TOP CMP,
TOP R0 GT MOV,
END-SUB

CODE MIN
R0 SP POP,
R0 TOP CMP,
TOP R0 LT MOV,
END-SUB

CODE ABS
TOP 0 # CMP,
TOP TOP 0 # MI RSB,
END-SUB


\ Memory primitives

CODE +!
R0 SP POP,
R1 TOP 0@ LDR,
R0 R0 R1 ADD,
R0 TOP 0@ STR,
TOP SP POP,
END-SUB


\ System primitives

CODE BYE
R0 0 # MOV,
R1 0 # MOV,
SWI," XOS_Exit"
END-CODE


\ Control primitives

CODE J
TOP SP PUSH,
TOP RP 8 #+@ LDR,
END-SUB
COMPILING

CODE EXIT
UNLINK,
END-CODE
COMPILING

CODE UNLOOP
RP RP 8 # ADD,
END-SUB
COMPILING

CODE EXECUTE
R0 TOP MOV,
TOP SP POP,
PC R0 MOV,
END-CODE

CODE @EXECUTE
R0 TOP MOV,
TOP SP POP,
PC R0 0@ LDR,
END-CODE


\ Extras

\ Arithmetic and logic

CODE ARSHIFT
R0 SP POP,
TOP R0 TOP ASR MOV,
END-SUB
