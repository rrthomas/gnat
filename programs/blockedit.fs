\ Block editor from "Forth Techniques" by Richard Olney & Michael Benson
\ Converted to ANSI (hopefully) and debugged by Reuben Thomas 1995

VOCABULARY EDITOR  ALSO EDITOR DEFINITIONS  DECIMAL

VARIABLE CURSOR
: START   ( -- adr )   SCR @ BLOCK ;
: REST   ( -- #bytes )   /BLOCK CURSOR @ - ;
: 'CURSOR   ( -- adr )   CURSOR @ START + ;
: END   ( -- adr )   'CURSOR REST -TRAILING + ;
: 'LINE   ( n -- adr )   /L * START + ;

2 CONSTANT TOP  8 CONSTANT MARGIN  L/BLOCK 1- CONSTANT LAST
VARIABLE SCREEN
: LINE   ( -- n )   SCREEN C@ ;
: COL   ( -- n )   SCREEN CHAR+ C@ ;
: COLS   ( -- n )   /L COL - ;
: GO   ( -- )   COL MARGIN +  LINE TOP +  AT-XY ;

: SET   ( -- )   CURSOR @ /L /MOD  SCREEN SWAP OVER C!
   CHAR+ C! ;
: +CURSOR   ( n -- )   CURSOR  DUP @ ROT + /BLOCK MOD  SWAP !
   SET ;
: HOME   0 DUP  CURSOR ! SCREEN ! ;

: DOWN   /L +CURSOR ;
: UP   /L NEGATE +CURSOR ;
: LEFT   -CHAR +CURSOR ;
: RIGHT   +CHAR +CURSOR ;
: RET   LINE LAST < IF  COLS +CURSOR  ELSE [ LAST /L * ]
   LITERAL  CURSOR !  THEN  SET ;

: .LINE   ( n -- )   'LINE /L TYPE ;
: .REMNANT   'CURSOR COLS TYPE ;
: .REST   SCREEN LINE  .REMNANT  LAST CHAR+ OVER CHAR+
   DO I SCREEN C!  GO LINE .LINE  LOOP  SWAP C! ;
: .BLOCK   HOME GO .REST ;

: .BLK   MARGIN 0 AT-XY ." Editing block: "  SCR @ 3 .R ;
: .LIN   43 0 AT-XY ." Line: "  LINE 2 .R ;
: .COL   58 0 AT-XY ." Column: "  COL 2 .R ;
: .INFO   .BLK .LIN .COL ;
: .HEAD   .BLOCK .INFO ;

: 2CONSTANT   CREATE 2,  DOES>  2@ ;
32 127 2CONSTANT RANGE
: ALLOWED   ( c -- f )   RANGE WITHIN ;
: CONTROL   ( c -- c f )   DUP BL <  OVER 126 >  OR ;

: ARRAY   ( n -- )   CREATE  CELLS ALLOT  DOES>  SWAP CELLS + ;
140 ARRAY FUNCTIONS
: FUNCTION   ( n -- )   FUNCTIONS @EXECUTE ;

: USES   ( n -- )   '  SWAP FUNCTIONS ! ;
139 USES UP   138 USES DOWN  137 USES RIGHT  136 USES LEFT
 30 USES HOME  13 USES RET

: PLACE  ( c -- )   DUP 'CURSOR C!  EMIT  UPDATE ;
: PART  ( -- )   'CURSOR DUP 1+  COLS CHAR-  CMOVE> ;
: INSERT   ( c -- )   DUP ALLOWED IF  PART PLACE RIGHT .REMNANT
   ELSE DROP  THEN ;
: DRAG   ( -- )   COLS 'CURSOR  2DUP DUP CHAR- ROT CMOVE  SWAP
   1-  -TRAILING CHARS +  BL SWAP C!  UPDATE ;
: DELETE   ( -- )   COL IF  DRAG LEFT GO .REMNANT  ELSE 7 EMIT
   THEN ;
127 USES DELETE
  8 USES DELETE

VARIABLE DONE
: ENOUGH   DONE @ ;
: ESCAPE   TRUE DONE ! ;
17 USES ESCAPE

: STARTING   FALSE DONE !  PAGE .HEAD  HOME .BLOCK ;

100 CONSTANT SIZE
1 CONSTANT LOWEST
: NEXT   SCR DUP @ 1+  SIZE  MIN  SWAP !  STARTING ;
: BACK   SCR DUP @ 1-  LOWEST MAX  SWAP !  STARTING ;
14 USES NEXT
 2 USES BACK

: WIPE   START  COL /L * +  /L BLANK  UPDATE .BLOCK ;
23 USES WIPE

FORTH DEFINITIONS  EDITOR
: EDIT   ( n -- )   SCR !  4 R0! 1 R1! [ 6 ] OS  STARTING  BEGIN
   .INFO  GO KEY  CONTROL IF  FUNCTION  ELSE INSERT  THEN
   ENOUGH UNTIL  0 18 AT-XY  4 R0! 0 R1! [ 6 ] OS ;
PREVIOUS